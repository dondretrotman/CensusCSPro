{Application 'MENU' logic file generated by CSPro}
PROC GLOBAL
//-----------------Variable Declaration-------------------
file	pffFile;
numeric EDNUM;
numeric PAR;
string	cases;

//-------------------Array Declaration--------------------
array string vsCode(99);
array string vsLabel(99);

//------------------Function Declaration------------------
//Launches the visitation record
function launchVR()
	// ALW - Added stop
	execpff("..\Visitation record\Visitation record.pff", stop);
end;

//Launches the Visitation Record for Supervisors
function launchSupervisorVR()

	string pffFilename = pathname(application) + "..\Visitation Record\Visitation Record.pff";
	setfile(pffFile,pffFilename,create);

	filewrite(pffFile,"[Run Information]");
	filewrite(pffFile,"Version=CSPro 7.1");
	filewrite(pffFile,"AppType=Entry");

	filewrite(pffFile,"[DataEntryInit]");
	
	filewrite(pffFile,"StartMode=Add;%s",CHOOSE_VR);
	filewrite(pffFile,"ShowInApplicationListing=Hidden");

	filewrite(pffFile,"[Files]");
	filewrite(pffFile,"Application=%s","./Visitation Record.ent");
	filewrite(pffFile,"InputData=%s","../Data/VisitationRecord.csdb|CSPRODB");
	filewrite(pffFile,"Paradata=%s","./Visitation Record.cslog");

	filewrite(pffFile,"[Parameters]");
	filewrite(pffFile,"Parameter=%s","onexit = ..\Menu\Menu.pff");

	filewrite(pffFile,"VI_PARISH=%S",CHOOSE_VR[1:2]);		//CHOOSE_HOUSEHOLD[X:Y] where x = starting position, y = length
	filewrite(pffFile,"VI_ED=%S",CHOOSE_VR[3:3]);
	filewrite(pffFile,"VI_BUILDING=%S",CHOOSE_VR[6:3]);
	filewrite(pffFile,"VI_DWELLING_UNIT=%S",CHOOSE_VR[9:3]);
	filewrite(pffFile,"VI_HOUSEHOLD=%S",CHOOSE_VR[12:3]);
	
	close(pffFile);

	execpff(filename(pffFile), stop);

end;


//Launches the Questionnaire and lets you choose a household
function launchQuestionnaire()

	string pffFilename = pathname(application) + "..\Data Entry\Census2020Ver2.pff";
	setfile(pffFile,pffFilename,create);

	filewrite(pffFile,"[Run Information]");
	filewrite(pffFile,"Version=CSPro 7.1");
	filewrite(pffFile,"AppType=Entry");
	filewrite(pffFile,"Description=Barbados 2020 Census Questionnaire");

	filewrite(pffFile,"[DataEntryInit]");
	
	// ALW - The below line will add the case if it does not exist
	// or enter the case in modify mode if it does
	filewrite(pffFile,"StartMode=Add;%s",CHOOSE_HOUSEHOLD);
	filewrite(pffFile,"ShowInApplicationListing=Hidden");

	filewrite(pffFile,"[Files]");
	filewrite(pffFile,"Application=%s","./Census2020Ver2.ent");
	filewrite(pffFile,"InputData=%s","../Data/CensusData.csdb|CSPRODB");
	filewrite(pffFile,"Paradata=%s","./Census2020Ver2.cslog");

	filewrite(pffFile,"[ExternalFiles]");
	filewrite(pffFile,"VISITATION_RECORD_DICT=%s","../Data/VisitationRecord.csdb|CSPRODB");

	filewrite(pffFile,"[UserFiles]");
	filewrite(pffFile,"PFFFILE=%s","");

	filewrite(pffFile,"[Parameters]");
	filewrite(pffFile,"Parameter=%s","onexit = ..\Menu\Menu.pff");
	
	filewrite(pffFile,"PARISH=%S",CHOOSE_HOUSEHOLD[1:2]);		//CHOOSE_HOUSEHOLD[X:Y] where x = starting position, y = length
	filewrite(pffFile,"ED=%S",CHOOSE_HOUSEHOLD[3:3]);
	filewrite(pffFile,"BUILDING=%S",CHOOSE_HOUSEHOLD[6:3]);
	filewrite(pffFile,"DWELLING_UNIT=%S",CHOOSE_HOUSEHOLD[9:3]);
	filewrite(pffFile,"HOUSEHOLD=%S",CHOOSE_HOUSEHOLD[12:3]);

	close(pffFile);

	// ALW - Added stop
	execpff(filename(pffFile), stop);

end;

//Function to upload all Census and VR data
function uploadData()
	if syncconnect(Dropbox) then		//connect to dropbox
		syncdata(PUT,Census2020_DICT);	//Upload Census data
		syncdata(PUT,VISITATION_RECORD_DICT);	//Upload VR
		syncfile(PUT,"../Data Entry/Census2020Ver2.cslog","/CSPro/CensusLogs/censuslog.cslog");	//upload Questionnaire log file
		syncfile(PUT,"../Visitation record/Visitation Record.cslog","/CSPro/VRLogs/vrlog.cslog");	//upload VR log
		syncfile(PUT,"../Menu/Menu.cslog","/CSPro/MenuLogs/Menulog.cslog");		//Upload menu log
		syncdisconnect();
	endif;
end;

//Function to download Census and VR data for an ED
function synchroniseDAta()
	recode EDNUM => PAR;
		1-181	=>	1;
		182-583	=>	2;
	endrecode;

	cases = maketext("%V%V",EDNUM,PAR);		//Get ED number TODO: Its hard coded for now, need to create a dictionary item that gets this from the login
	if syncconnect(Dropbox) then	//connect to dropbox
		syncdata(BOTH,Census2020_DICT,cases);		//download Census Data, TODO: may need to tweak the universe  
		syncdata(BOTH,VISITATION_RECORD_DICT,cases);	//Download VR
		syncDisconnect();
	endif;
end;

PROC MENU_FF

PROC UNAME
//add code to make sure that what is typed is in the valueset
PROC USER_PIN
//add code to make sure that the valueset label matches Username
PROC LOGIN

	if $ = 1 then	//interviewer
		skip to INTERVIEWER_MENU;
	elseif $ = 2 then 	//supervisor
		skip to SUPERVISOR_MENU;
	elseif $ = 3 then	//senior supervisor
	
	elseif $ = 4 then	//zone manager
	
	elseif $ = 9 then
		stop(1);
	endif;
PROC INTERVIEWER_MENU
onfocus
	$ = notappl;	//don't show the last selection
	
postproc
	if $ = 1 then		//Visitation Record
		launchVR();	
	elseif $ = 2 then	//interview households
		skip to CHOOSE_HOUSEHOLD;		//DEBUG: AN ERROR APPEARS AT THIS POINT
	elseif $ = 3 then	//upload data
		uploadData();
	elseif $ = 9 then	//logout
		stop(1);
	endif;
reenter;		//don't leave the menu
PROC SUPERVISOR_MENU
onfocus
	$ = notappl;	//don't show the last selection
	
postproc
	if $ = 1 then		//Download/upload Data
		synchroniseData();
	elseif $ = 2 then	//Open VR
		skip to CHOOSE_VR;		//DEBUG: AN ERROR APPEARS AT THIS POINT
	elseif$ = 3 then
		skip to CHOOSE_HOUSEHOLD;
	elseif $ = 9 then	//logout
		stop(1);
	endif;
reenter;		//don't leave the menu
PROC CHOOSE_HOUSEHOLD

onfocus
	numeric nextEntry = 1;
	forcase VISITATION_RECORD_DICT do
		vscode(nextEntry) = maketext("%v%v%v%v%v",VI_PARISH,VI_ED,VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD);
		vsLabel(nextEntry) = maketext("%v%v%v: %V (%v)",VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD,strip(VI_LAST_NAME),getlabel(VI_STATUS_VS1,VI_STATUS));
		inc(nextEntry);
	endfor;
	
	vsCode(nextEntry) = "";
	
	setvalueset($, vsCode, vsLabel);

postproc
	launchQuestionnaire();
	move to LOGIN;
PROC CHOOSE_VR

onfocus
	numeric nextEntry = 1;
	forcase VISITATION_RECORD_DICT do
		vscode(nextEntry) = maketext("%v%v%v%v%v",VI_PARISH,VI_ED,VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD);
		vsLabel(nextEntry) = maketext("%v%v%v: %V (%v)",VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD,strip(VI_LAST_NAME),getlabel(VI_STATUS_VS1,VI_STATUS));
		inc(nextEntry);
	endfor;
	
	vsCode(nextEntry) = "";
	
	setvalueset($, vsCode, vsLabel);

postproc
	launchSupervisorVR();
	move to LOGIN;
