{Application 'VISITATION_RECORD' logic file generated by CSPro}
PROC GLOBAL

numeric i;	//counter

//Checks that date is valid. Returns 1 if valid. Returns 0 if invalid
function checkDate(YEAR, MONTH, DAY)
	if not YEAR in 1900:2020,9999 then
		errmsg("Invalid Year. (=%d)",YEAR);
		checkDate = 0;
	endif;
	
	if not MONTH in 1:12,99 then
		errmsg("Invalid Month. (=%d)",Month);
		checkDate = 0;
	endif;
	
	if MONTH in 9,4,6,11 then		//30 days have September, April, June and November
		if not DAY in 1:30,99 then
			errmsg("%s does not have %d days. Please confirm.",getlabel(VI_MONTH_STARTED_VS1,MONTH),DAY);
			checkDate = 0;
		endif;
	elseif MONTH in 1,3,5,7,8,10,12 then		//All the rest have 31
		if not DAY in 1:31,99 then
			errmsg("%s does not have %d days. Please confirm.",getlabel(VI_MONTH_STARTED_VS1,MONTH),DAY);
			checkDate = 0;
		endif;
	elseif MONTH = 2 and YEAR in 1900:2020 then					//Except February alone
		if YEAR % 4 = 0 and YEAR <> 1900 then		
			if not DAY in 1:29,99 then		//Which has 29 days each leap year
				errmsg("%s does not have %d days. Please confirm.",getlabel(VI_MONTH_STARTED_VS1,MONTH),DAY);
				checkDate = 0;
			endif;	
		else
			if not DAY in 1:28,99 then		//And 28 on all other years
				errmsg("%s does not have %d days. Please confirm.",getlabel(VI_MONTH_STARTED_VS1,MONTH),DAY);
				checkDate = 0;
			endif;		
		endif;		//I had to change the rhyme a bit so that it would fit my code
	elseif YEAR = 9999 then		//cleanup for unknown year
		if MONTH = 2 then
			if not DAY in 1:29,99 then
				errmsg("%s does not have %d days. Please confirm.",getlabel(VI_MONTH_STARTED_VS1,MONTH),DAY);
				checkDate = 0
			endif;
		endif;
	else
		checkDate = 1;
	endif;
end;

//Function uses device's GPS feature to get Location
function getgps()
	gps(open);
	if gps(read,60,10,"Reading GPS, Please wait...") = 1 then	//read up to 60 seconds. if successful...
		execsystem(maketext("gps:%f,%f", gps(latitude),gps(longitude)));
		if accept("Save the location?","Yes","No") = 1 then	//if yes
			VI_LATITUDE = gps(latitude);
			VI_LONGITUDE = gps(longitude);
		endif;
	else
		errmsg("gps location failed");
		VI_LATITUDE=0;
		VI_LONGITUDE = 0;
	endif;
	gps(close);
end;

{PROC VI_LATITUDE
onfocus
	if $ <> notappl and VI_LONGITUDE <> 0 then
		advance to VI_COMMENTS
	else
		errmsg("Please press 'Get Location' in the navbar");
		$ = 0;
		VI_LONGITUDE = 0;
	endif;
}

//------------------------------------------------------------------------------------------------
PROC VISITATION_RECORD_FF
preproc
	userbar(clear);
	userbar(add button, "Get Location", getgps());		//Adds a Get Location button to get coordinates
	userbar(show);

PROC VISITATION_RECORD_QUEST
postproc
	//mandatory longitude and latitude capture
	if visualvalue(VI_LATITUDE) in 0,notappl,default or visualvalue(VI_LONGITUDE) in 0,notappl,default then
		getgps();
	endif;
	
	//if latitude or longitude are invalid, prompt to updatefield. If valid, ask if to leave coordinates or to update
	i=0;
	do until i in 1,2
		i = accept(maketext("Accept current location before proceeding to the next dwelling unit (%f,%f)?",VI_LATITUDE,VI_LONGITUDE),"Get current location again","Accept current location");
	enddo;
	if i = 1 then
		getgps();		//get location
	elseif i = 2 then
		//break;			//cancel the while loop
	endif;
PROC VI_PARISH
preproc
	if visualvalue(VI_PARISH) = notappl then		//if Parish is blank
		VI_PARISH = tonumber(sysparm("PARISH"));		//Go to the pff file and get the system parameter named Parish. Change it to number and assign to variable
		setproperty(VI_PARISH,"PROTECTED","Yes");	//and set province to proctected
	endif;
	if visualvalue(VI_ED) = notappl then	
		VI_ED = tonumber(sysparm("ED"));
		setproperty(VI_ED,"PROTECTED","Yes");	
	endif;
	
	if visualvalue(VI_ENUMERATOR) = notappl then
		VI_ENUMERATOR = tonumber(sysparm("USERID"));
	endif;
	//TODO: pull supervisor ID from allocations app

//------------------------------------------------------------------------------
PROC VI_ED
//validate ED with parish
	if $ in 1:181 and VI_PARISH <> 1 or		//if ed is not in St. Michael
		$ in 182:295 and VI_PARISH <> 2 or 	//or Christ Church
		$ in 296:335 and VI_PARISH <> 3 or		//or St. George
		$ in 336:397 and VI_PARISH <> 4 or 	//or St. Philip
		$ in 398:418 and VI_PARISH <> 5 or 	//or St. John
		$ in 419:479 and VI_PARISH <> 6 or 	//or St. James
		$ in 480:508 and VI_PARISH <> 7 or 	//or St. Thomas
		$ in 509:521 and VI_PARISH <> 8 or 	//or St. Joseph
		$ in 522:532 and VI_PARISH <> 9 or 	//or St. Andrew
		$ in 533:559 and VI_PARISH <> 10 or 	//or St. Peter
		$ in 560:583 and VI_PARISH <> 11 then	//or St. Lucy
		errmsg("Enumeration district (%d) does not match the VI_PARISH (%s)",$,getlabel(VI_PARISH_VS1,VI_PARISH))
			select("Correct PARISH",VI_PARISH,"Correct ED",$);
	endif;

//----------------------------------------------------------------------------------
PROC VI_DATE_STARTED
preproc
	$ = sysdate("YYYYMMDD");

postproc
	if checkdate(VI_YEAR_STARTED,VI_MONTH_STARTED,VI_DAY_STARTED) = 0 then
		reenter;
	endif;

//-----------------------------------------------------------------------------------------
PROC VI_LAST_NAME
//Set case labels in case listings to an easier to read format to include name of head
	string caselabel = maketext("%s-%v-%v-%v-%v-%s",strip(VI_LAST_NAME),VI_ED,VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD,getlabel(VI_STATUS_VS1,VI_STATUS));
	setcaselabel(VISITATION_RECORD_DICT,caselabel);

PROC VI_TOTAL
onfocus
	$ = VI_MALES + VI_FEMALES;
postproc
	if $ > VI_MALES + VI_FEMALES then
		errmsg("Number of males (%d) and number of females (%d) don't agree with total (%d)",VI_MALES,VI_FEMALES,VI_TOTAL)
			select("Correct Males",VI_MALES,"Correct Females",VI_FEMALES,"Correct Total",VI_TOTAL);
	endif;

//------------------------------------------------------------------------------------------------------
PROC VI_DATE_COMPLETED
onfocus
	$ = sysdate("YYYYMMDD");
	
postproc
	if checkdate(VI_YEAR_COMPLETED,VI_MONTH_COMPLETED,VI_DAY_COMPLETED) = 0 then
		reenter;
	endif;

//-----------------------------------------------------------------------------------------
