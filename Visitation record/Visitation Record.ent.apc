{Application 'VISITATION_RECORD' logic file generated by CSPro}
PROC GLOBAL

//Function uses device's GPS feature to get Location
function getgps()
	gps(open);
	if gps(read,60,10,"Reading GPS, Please wait...") = 1 then	//read up to 60 seconds. if successful...
		execsystem(maketext("gps:%f,%f", gps(latitude),gps(longitude)));
		if accept("Save the location?","Yes","No") = 1 then	//if yes
			VI_LATITUDE = gps(latitude);
			VI_LONGITUDE = gps(longitude);
			advance to VI_LONGITUDE;
		endif;
	else
		errmsg("gps location failed");
		VI_LATITUDE=0;
		VI_LONGITUDE = 0;
	endif;
	gps(close);
end;

PROC VISITATION_RECORD_FF
preproc
	userbar(clear);
	userbar(add button, "Get Location", getgps());		//Adds a Get Location button to get coordinates
	userbar(show);

PROC VI_ED
//validate ED with parish
	if $ in 1:181 and VI_PARISH <> 1 or		//if ed is not in St. Michael
		$ in 182:295 and VI_PARISH <> 2 or 	//or Christ Church
		$ in 296:335 and VI_PARISH <> 3 or		//or St. George
		$ in 336:397 and VI_PARISH <> 4 or 	//or St. Philip
		$ in 398:418 and VI_PARISH <> 5 or 	//or St. John
		$ in 419:479 and VI_PARISH <> 6 or 	//or St. James
		$ in 480:508 and VI_PARISH <> 7 or 	//or St. Thomas
		$ in 509:521 and VI_PARISH <> 8 or 	//or St. Joseph
		$ in 522:532 and VI_PARISH <> 9 or 	//or St. Andrew
		$ in 533:559 and VI_PARISH <> 10 or 	//or St. Peter
		$ in 560:583 and VI_PARISH <> 11 then	//or St. Lucy
		errmsg("Enumeration district (%d) does not match the VI_PARISH (%s)",$,getlabel(VI_PARISH_VS1,VI_PARISH))
			select("Correct PARISH",VI_PARISH,"Correct ED",$);
	endif;

//----------------------------------------------------------------------------------
PROC VI_LAST_NAME
//Set case labels in case listings to an easier to read format to include name of head
	string caselabel = maketext("%s-%v-%v-%v-%v-%s",strip(VI_LAST_NAME),VI_ED,VI_BUILDING,VI_DWELLING_UNIT,VI_HOUSEHOLD,getlabel(VI_STATUS_VS1,VI_STATUS));
	setcaselabel(VISITATION_RECORD_DICT,caselabel);

PROC VI_TOTAL
onfocus
	$ = VI_MALES + VI_FEMALES;
postproc
	if $ <> VI_MALES + VI_FEMALES then
		errmsg("Number of males (%d) and number of females (%d) don't agree with total (%d)",VI_MALES,VI_FEMALES,VI_TOTAL)
			select("Correct Males",VI_MALES,"Correct Females",VI_FEMALES,"Correct Total",VI_TOTAL);
	endif;

//------------------------------------------------------------------------------------------------------
PROC VI_LATITUDE
onfocus
	if $ <> notappl and VI_LONGITUDE <> 0 then
		advance to VI_COMMENTS
	else
		errmsg("Please press 'Get Location' in the navbar");
		$ = 0;
		VI_LONGITUDE = 0;
	endif;


//------------------------------------------------------------------------------------------------
