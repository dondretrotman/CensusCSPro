{Application 'CENSUS2020VER2' logic file generated by CSPro}
{
Census Android CAPI application
Author: Dondré Trotman
Date: 2018/05/17
Version: 0.1

Description:
This application is meant to be installed on an Android device using the CSPRO app available on the Google Play Store.
It is based on the 2020 Barbados Population and Housing Census questionnaire which is still under development.
To run on desktop, it needs the following file extensions, all with the same name:
.ent, .net.apc, .ent.mgf, .ent.qsf, .fmf, .pff
To run on Android, it needs for following file extensions, all with the same name:
.pff, .pen

Special instructions: 
Make sure that the order of the individual Listing and the population section are the same.

TODO:
Add more edit checks
Make sure that android experience is good
use toupper() (or tolower()) functions to ensure that all alpha variables are the same case

Questions for the committee\Cecsus bureau:
Is a spouse required to be married? Yes
What is the best way to handle NOT STATED? Do we continue to questions that might have otherwise been skipped or do we skip to the next topic?
}

PROC GLOBAL
//--------------------------VARIABLE DECLARATION-------------------------------
numeric i, tempage, residents, nonreshouse, diff;

PROC CENSUS2020_FF

PROC IID
preproc
	$ = curocc();		//automatically enter the ID
	noinput;

postproc
	if curocc() = 1 then	//if at the first person
		RTH = 0;				//set to head of household
	endif;
	
//----------------------------------------------------------------------
PROC FIRST_NAME
POSTPROC
	setocclabel(POPULATION000(CUROCC()), strip(FIRST_NAME));

//------------------------------------------------------------
PROC AGE
preproc
	tempage = datediff(DOB, 20200501, "Y");
	
	if datediff(DOB,20200501) < 0 then		//if DOB 
		errmsg("This person is too young to be enumerated");
	endif;
	
	$ = tempage;	//calculate age

postproc	
	if $ <> tempage then
		errmsg("age (%d) does not agree with DOB (%s), please verify",AGE,edit("9999/99/99",DOB))
		select("Correct the age", AGE, "Correct the Date of birth", DOB);
	endif;

//----------------------------------------------------------------------
PROC RTH
preproc
	if curocc() = 1 then 	//if this is the first person
		errmsg("Please verify that this person is the head of household")
			select("Yes, this is the head", continue, "No, is not the head", LAST_NAME);
		$ = 0;	//make first person the head of household
		noinput;
	endif;
	
postproc	
	if $ = 1 and curocc() <> 1 then		//if head and person is not on the first record
		errmsg("Only one head of household is allowed. Current head of household is %s %s",strip(FIRST_NAME),strip(LAST_NAME));
	endif;

//----------------------------------------------------------------------
PROC ADDPER
	if $ = 2 then
		HNOPERSONS = totocc(POPULATION000);
		endgroup;
	endif;

//----------------------------------------------------------------------
PROC H8A
//H8(a) – Do you rent a room in this Building?
	if $ = 2 then
		skip to H8C;
	endif;
	
//----------------------------------------------------------------------
PROC H8B
//H8(b) – How many rented rooms are in this Building?
	if $ = 0 then	//if number of rooms is 0
		H8A = 2;	//assume no rented rooms
	elseif $ > 0 then	//if number of rented rooms is more than 0
		H8A = 1; 	//assume rented rooms
	endif;
	
//----------------------------------------------------------------------
PROC H8C
//H8(c) – What type of dwelling unit is this?
	if H8A = 1 then		//if rented rooms exist
		skip to H14A	//goto H14A
	endif;

//----------------------------------------------------------------------
PROC H9
//H9 – Of what materials are the outer walls made?
//should checks on the consistency of Walls, roofs, floors and foundations be done afterward?
PROC H14A
	if $ = 1 then		//if occupied
		skip to H14C	//goto H14C
	endif;

//----------------------------------------------------------------------
PROC H14C
	if H14A = 2 then	//if unoccupied
		endlevel;		//end interview MAY NEED TO REVIEW FOR CORRECT KEYWORD
	endif;
	
	//if occupied by only non residents (USUAL_RESIDENCE = A)
	//go to questions H15, H16 & H20 - H22

	//count non residents
	if H14A = 1 then		//if occupied
		residents = 0;
		nonreshouse = 0;
		do varying i = 1 until i > totocc(POPULATION000)
			if UR(i) = 1 then		//if resident is found
				inc(residents);	//count residents
			endif;
		enddo;
		if residents = 0 then	//if all persons are not usually resident in this house
			nonreshouse = 1;	//set non-resident house flag
			skip to H15;		//skip to H15
		endif;
	endif;

//----------------------------------------------------------------------
PROC H16C
preproc

postproc
	if nonreshouse = 1 then		//if non-resident household flag is set
		skip to H20;			//go to Main water supply
	endif;
	
//------------------------------------------------------------
PROC H17B
	if not H17A in 3:4 then		//If not rented or leased
		skip to H19A;		//goto H19A
	endif;

//----------------------------------------------------------------------
PROC H18
	skip to H20;	//goto H20

//----------------------------------------------------------------------
PROC H22C
preproc

postproc
	if nonreshouse = 1 then		//if non-resident household flag is set
		endlevel;				//end this questionnaire
	endif;
	
//------------------------------------------------------------
PROC H23AII
preproc
	ask if H8A = 1 and H8C in 1,3;	//only ask if this is a rented room in a separate house\apartment

//----------------------------------------------------------------------------
PROC H25B
	if pos("i",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;

//-------------------------------------------------------------------------
PROC H25C
	if pos("i",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;

//-------------------------------------------------------------
PROC E8A
	if $ = 2 then		//if no emmigrants
		endgroup;		//end emmigration section
	endif;
PROC POPULATION001

PROC PID
preproc
	do numeric idx = 1 while idx <= totocc(PID)
		setocclabel(POPULATION001(idx), strip(FIRST_NAME));
	enddo;
	
onfocus
	$ = curocc();


//-----------------------------------------------------------
PROC P12AII
onfocus
	if P12AI = 2 then
		skip to P14;
	endif;

postproc
	if pos("u",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;

//-------------------------------------------------------------------------------
PROC P12C
	if pos("h",$) > 0 and length($) > 1 then
		errmsg("cannot be none and another option");
		reenter;
	endif;
	
	if pos("e",$) > 0 and (pos("k",P12AII) = 0 or pos("l",P12AII) = 0 or pos("m",P12AII) = 0 or pos("n",P12AII) = 0 or pos("r",P12AII) = 0 or pos("s",P12AII) = 0 or pos("t",P12AII) = 0) then
		errmsg("you are using a prosthesis. Are you sure you don't have one of the following disabilities?\nAmputated Limbs\nLimb Deformities");
		//GO BACK TO P12AII OR DESELECT PROSTHESIS
	endif;
	
	if pos("g",$) > 0 and (pos("a",P12AII) = 0 or pos("b",P12AII) = 0) then	//if using Hearing aids but no deafness\significant hearing impairment
		errmsg("You use hearing aids. Are you sure that you are not deaf or have a significant hearing impairment?");	//make sure that person has deafness\significant hearing impairment
		//GO BACK TO P12AII OR DESELECT HEARING AIDS
	endif;

//------------------------------------------------------------------------------------
PROC P14
	if pos("h",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
PROC P15A
	if pos("d",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
	
	if pos("d",$) > 0 then	//if none
		$ = "d";			//set all to none
		P15B = "d";
		P15C = "d";
		skip to P16A;		//skip drug use
	endif;

//----------------------------------------------------------------
PROC P15B
	if pos("d",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
	
	if pos("a",$) > 0 and pos("a",P15A) = 0 and pos("z",P15A) = 0 then		//if used Alcohol in the last 12 months but never used alcohol
		errmsg("Are you sure? Alcohol was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;
	
	if pos("b",$) > 0 and pos("b",P15A) = 0 and pos("z",P15A) = 0 then		//if used Marijuana in the last 12 months but never used Marijuana
		errmsg("Are you sure? Marijuana was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;

	if pos("c",$) > 0 and pos("c",P15A) = 0 and pos("z",P15A) = 0 then		//if used Cocaine in the last 12 months but never used Cocaine
		errmsg("Are you sure? Cocaine was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;
	
	if pos("z",P15A) > 0 and (pos("z",$) = 0 or pos("d",$) = 0) and length($) > 0 then	//if not stated in P15A and used drugs in last 12 months
		//TODO: add $ to p15a using some kind of string functions
	endif;
	
	if pos("d",$) > 0 then	//if none
		$ = "d";			//set all to none
		P15C = "d";
		skip to P16A;		//skip drug use
	endif;

//-----------------------------------------------------------------
PROC P15C
	if pos("d",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
	
	if pos("a",$) > 0 and pos("a",P15A) = 0 and pos("z",P15A) = 0 then		//if used Alcohol in the last 12 months but never used alcohol
		errmsg("Are you sure? Alcohol was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;
	
	if pos("b",$) > 0 and pos("b",P15A) = 0 and pos("z",P15A) = 0 then		//if used Marijuana in the last 12 months but never used Marijuana
		errmsg("Are you sure? Marijuana was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;

	if pos("c",$) > 0 and pos("c",P15A) = 0 and pos("z",P15A) = 0 then		//if used Cocaine in the last 12 months but never used Cocaine
		errmsg("Are you sure? Cocaine was used in last 12 months but it was also never used")
			select("Correct ever used",P15A,"Correct use in last 12 months",$);
	endif;
	
	if pos("z",P15A) > 0 and (pos("z",$) = 0 or pos("d",$) = 0) and length($) > 0 then	//if not stated in P15A and used drugs in last 30 days
		//TODO: add $ to p15a using some kind of string functions
	endif;
	
	if pos("d",$) > 0 then	//if none
		$ = "d";			//set all to none
		skip to P16A;		//skip drug use
	endif;

//-----------------------------------------------------------------
PROC P16A
	if $ = 2 then		//if not born in barbados
		skip to P17A;	//skip migration
	endif;

//-----------------------------------------------------------------
PROC P16B
	if AGE < 1 then		//if less than a year old	//ADD CHECKS FOR DOB
		skip to P44;	//skip to last question
	else
		skip to P18;	//otherwise go to Living 1 year ago
	endif;

//-----------------------------------------------------------------------------
PROC P17B
//P17(b) - In which year did you first arrive in Barbados to live?
	diff = datediff(DOB_YEAR,$);
	if diff < 0 then	//if first arrived to live in barbados before birth year
		errmsg("Year first arrived(%d) is after birth year(%d)",$,DOB_YEAR);
		reenter;		//birth year is more likely to be correct at this point
	endif;

//-----------------------------------------------------------
PROC P18
	if AGE < 5 then		//if born after may 1, 2015 (Age 5 years)
		skip to P21A;	//Skip to Education
	endif;

//----------------------------------------------------------------------
PROC P19A
	if $ <> 3 then		//if was not living in another parish
		skip to P20A;	//skip to Ever left barbados
	endif;

//-------------------------------------------------------------
PROC P19B
//P19(b) - If IN ANOTHER PARISH, Which parish?
	if $ = PARISH then		//if current parish is the same as another parish
		errmsg("Another parish(%s) is the same as current parish(%s)",getlabel(P19B_VS1, P19B), getlabel(PARISH_VS1, PARISH))		//either change another parish or change P19A
			select("Change another parish",$,"Change address at 2015",P19A);
	endif;

//-----------------------------------------------------------------
PROC P20A
	if $ = 2 then		//If never left barbados for 5 years
		skip to P21A;	//skip to Education
	endif;
	
//-----------------------------------------------------------------
PROC P21A
onfocus
	if AGE < 3 then		//if under 3 years old
		skip to P44;	//Go to final question
	endif;
	
postproc	
	if AGE in 3:16 then		//if between 3 - 16 years old
		skip to P22;		//skip to P22
	endif;
	
	if AGE > 16 then		//if over 16 years old
		skip to P23;		//skip to P23
	endif;
	
//--------------------------------------------------------------------------
PROC P21B
//TODO: check that the AGE corresponds with current school
	if AGE < 15 then		//If under 15 years old
		skip to P44;		//skip to final question
	endif;
	
//-----------------------------------------------------------------
PROC P23
//P23 - what is the highest level of educational institution ever attended by you?
//TODO: check that highest level institution is not lower than P21B if still attending school
PROC P25AI
//TODO: check that examinations passed corresponds with P23
	if pos("a",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
	
	if pos("b",$) > 0 and length($) > 1 then
		errmsg("cannot be not stated and another option");
		reenter;
	endif;
	
//--------------------------------------------------------------------
PROC P26A
	if pos("i",$) > 0 and length($) > 1 then				//if both "Don't use" and another option
		errmsg("Cannot have Don't use and use internet");	
		reenter;											//input field again
	endif;
	
	if pos("i",$) > 0 then		//if don't use
		skip to P27AI;			//skip to training
	endif;
	
//------------------------------------------------------------
PROC P26B
//TODO: this answer must be found in P26A


//-------------------------------------------------------
PROC P27AI
	if $ = 2 and SEX = 2 then		//if never trained and female
		skip to P30A;				//skip to fertility
	elseif $ = 2 and SEX = 1 then	//if male
		skip to P34;				//skip to Economic activity
	endif;
	
//---------------------------------------------------------------------
PROC P27C
//P27(c) - If completed training, in what year did you complete training
//TODO: year completed training should be at least 15 years after year of birth

//-----------------------------------------------------------
PROC P29
	if (SEX = 2 and AGE > 64) or SEX = 1 then	//If female over 64 years or male
		skip to P34;							//skip to economic activity
	endif;
	
//----------------------------------------------------------------------------------
PROC P30A
onfocus		//sanity check to make sure that Females outside of childbearing age and Males don't answer
	if SEX <> 2 then
		skip to P34;
	elseif SEX = 2 and not AGE in 15:64 then
		skip to P34;
	endif;
	 
postproc
	if $ = 2 then	//if never had children
		skip to P33A	//skip to Union status
	endif;
	
//-------------------------------------------------------------------------
PROC P31D
	if AGE > 49 then	//if age is over 49
		skip to P33A;	//skip to union status
	endif;
	
//---------------------------------------------------------------
PROC P33A
	if $ = 2 then		//if not living with a partner
		skip to P33BII	//skip to P33BII
	endif;
	
//-----------------------------------------------------------
PROC P33BI
onfocus
	if P33A <> 1 or P9 <> 1 then	//if not living with a partner or not married
		skip to next;				//go to next question
	endif;
	
//---------------------------------------------------------------------------------
PROC P33BII
onfocus
	if P33A <> 1 then 	//if NO
		skip to P34		//skip to economic activity
	endif;
	
//-------------------------------------------------------------------
PROC P33C
onfocus
	if P33A = 2 then	//if not with husband or common law partner
		$ = 0;			//score as 0
		skip to P34;	//and skip to economic activity
	endif;
	
//----------------------------------------------------------------------
PROC P34
onfocus
	if AGE < 15 then	//if under 15 years old
		skip to P44;	//skip to final question
	endif;
	
//----------------------------------------------------------
PROC P35
	if $ = 8 then		//if did not work
		skip to P39;	//skip to sources of livelihood
	endif;

//--------------------------------------------------------------------
PROC P39
	if pos("l",$) > 0 and length($) > 1 then		//if Not Stated and other options selected
		errmsg("cannot be not stated and have other selections");
		reenter;		//inpute again
	elseif pos("k",$) > 0 and length($) > 1 then		//if None and other options selected
		errmsg("Cannot be non and have other selections");
		reenter;		//impute again
	endif;
	
	if P35 = 8 then		//If did not work
		skip to P40C;	//skip to Income
	endif;
	
//----------------------------------------------------------
PROC P40A
	if pos("k",P39) > 0 then	//if no sources of livelihood
		skip to P41A;	//skip to agriculture
	endif;
	
//------------------------------------------------------------
